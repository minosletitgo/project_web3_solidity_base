

#### 概念：
##### 一种数字货币钱包，它的特点在于需要多个预设的拥有者（或称为签署者）中的若干个来共同确认和签署交易，才能完成资金的转移(或其他操作)。

#### 设计目的
##### 为了提高资金安全性，防止未经授权的资金挪用，以及在一定程度上减少因单一密钥丢失而导致的资金损失风险。

#### 使用场景：
##### 企业财务管理、家庭共同基金管理和涉及多方利益相关者的场景中。
##### 个人资产管理的加强版工具(一个人可以将自己的资金分散存放在不同的私钥中，需要多个私钥的同时确认才能动用资金，以此来增加安全性)。

#### 其他：
- 多签钱包可以持有以太币，或者其他ERC代币，都行
- 多签钱包的交易目的地：普通账户地址、智能合约地址、另一个多签地址、等等

------------------------------------------------------------------------------------------------------------------------

　


　
#### 以下展示一个使用示例：
##### 假设有5位持有人（Alice、Bob、Charlie、Dave 和 Eric），并且设置为 4 签 3（至少需要 3 人签名才能执行交易），以下是完整展示。

- 完整的多签钱包合约
```
// SPDX-License-Identifier: MIT
// author: @0xAA_Science from wtf.academy
pragma solidity ^0.8.21;

/// 基于签名的多签钱包，由gnosis safe合约简化而来，教学使用。
contract TestMultisigWallet {
    event ExecutionSuccess(bytes32 txHash);    // 交易成功事件
    event ExecutionFailure(bytes32 txHash);    // 交易失败事件
    address[] public owners;                   // 多签持有人数组 
    mapping(address => bool) public isOwner;   // 记录一个地址是否为多签
    uint256 public ownerCount;                 // 多签持有人数量
    uint256 public threshold;                  // 多签执行门槛，交易至少有n个多签人签名才能被执行。
    uint256 public nonce;                      // nonce，防止签名重放攻击

    receive() external payable {}

    // 构造函数，初始化owners, isOwner, ownerCount, threshold 
    constructor(        
        address[] memory _owners,
        uint256 _threshold
    ) {
        _setupOwners(_owners, _threshold);
    }

    /// @dev 初始化owners, isOwner, ownerCount,threshold 
    /// @param _owners: 多签持有人数组
    /// @param _threshold: 多签执行门槛，至少有几个多签人签署了交易
    function _setupOwners(address[] memory _owners, uint256 _threshold) internal {
        // threshold没被初始化过
        require(threshold == 0, "WTF5000");
        // 多签执行门槛 小于 多签人数
        require(_threshold <= _owners.length, "WTF5001");
        // 多签执行门槛至少为1
        require(_threshold >= 1, "WTF5002");

        for (uint256 i = 0; i < _owners.length; i++) {
            address owner = _owners[i];
            // 多签人不能为0地址，本合约地址，不能重复
            require(owner != address(0) && owner != address(this) && !isOwner[owner], "WTF5003");
            owners.push(owner);
            isOwner[owner] = true;
        }
        ownerCount = _owners.length;
        threshold = _threshold;
    }

    /// @dev 在收集足够的多签签名后，执行交易
    /// @param to 目标合约地址
    /// @param value msg.value，支付的以太坊
    /// @param data calldata
    /// @param signatures 打包的签名，对应的多签地址由小到达，方便检查。 ({bytes32 r}{bytes32 s}{uint8 v}) (第一个多签的签名, 第二个多签的签名 ... )
    function execTransaction(
        address to,
        uint256 value,
        bytes memory data,
        bytes memory signatures
    ) public payable virtual returns (bool success) {
        // 编码交易数据，计算哈希
        bytes32 txHash = encodeTransactionData(to, value, data, nonce, block.chainid);
        nonce++;  // 增加nonce
        checkSignatures(txHash, signatures); // 检查签名
        // 利用call执行交易，并获取交易结果
        (success, ) = to.call{value: value}(data);
        require(success , "WTF5004");
        if (success) emit ExecutionSuccess(txHash);
        else emit ExecutionFailure(txHash);
    }

    /**
     * @dev 检查签名和交易数据是否对应。如果是无效签名，交易会revert
     * @param dataHash 交易数据哈希
     * @param signatures 几个多签签名打包在一起
     */
    function checkSignatures(
        bytes32 dataHash,
        bytes memory signatures
    ) public view {
        // 读取多签执行门槛
        uint256 _threshold = threshold;
        require(_threshold > 0, "WTF5005");

        // 检查签名长度足够长
        require(signatures.length >= _threshold * 65, "WTF5006");

        // 通过一个循环，检查收集的签名是否有效
        // 大概思路：
        // 1. 用ecdsa先验证签名是否有效
        // 2. 利用 currentOwner > lastOwner 确定签名来自不同多签（多签地址递增）
        // 3. 利用 isOwner[currentOwner] 确定签名者为多签持有人
        address lastOwner = address(0); 
        address currentOwner;
        uint8 v;
        bytes32 r;
        bytes32 s;
        uint256 i;
        for (i = 0; i < _threshold; i++) {
            (v, r, s) = signatureSplit(signatures, i);
            // 利用ecrecover检查签名是否有效
            currentOwner = ecrecover(keccak256(abi.encodePacked("\x19Ethereum Signed Message:\n32", dataHash)), v, r, s);
            require(currentOwner > lastOwner && isOwner[currentOwner], "WTF5007");
            lastOwner = currentOwner;
        }
    }
    
    /// 将单个签名从打包的签名分离出来
    /// @param signatures 打包的多签
    /// @param pos 要读取的多签index.
    function signatureSplit(bytes memory signatures, uint256 pos)
        internal
        pure
        returns (
            uint8 v,
            bytes32 r,
            bytes32 s
        )
    {
        // 签名的格式：{bytes32 r}{bytes32 s}{uint8 v}
        assembly {
            let signaturePos := mul(0x41, pos)
            r := mload(add(signatures, add(signaturePos, 0x20)))
            s := mload(add(signatures, add(signaturePos, 0x40)))
            v := and(mload(add(signatures, add(signaturePos, 0x41))), 0xff)
        }
    }

    /// @dev 编码交易数据
    /// @param to 目标合约地址
    /// @param value msg.value，支付的以太坊
    /// @param data calldata
    /// @param _nonce 交易的nonce.
    /// @param chainid 链id
    /// @return 交易哈希bytes.
    function encodeTransactionData(
        address to,
        uint256 value,
        bytes memory data,
        uint256 _nonce,
        uint256 chainid
    ) public pure returns (bytes32) {
        bytes32 safeTxHash =
            keccak256(
                abi.encode(
                    to,
                    value,
                    keccak256(data),
                    _nonce,
                    chainid
                )
            );
        return safeTxHash;
    }

    // 查询余额
    function getBalance() public view returns (uint256) {
        return address(this).balance;
    }    
}
```

　

- 部署钱包
- 操作人：任意一位持有人（如 Alice）
- nodejs代码如下，deployTestMultisigWallet.js
```
const hre = require("hardhat");
const logger = require('../srcs/logger');
const assert = require('assert');

async function main() {
    const signers = await hre.ethers.getSigners(); // 确保配置了5个账户
    logger.info(`Total accounts available: ${signers.length}`);

    const ownerCount = 5;
    const threshold = 3;
    assert.strictEqual(signers.length, ownerCount, 'signers.length should be 5');
   
    const adr_Alice = signers[0].address;
    const adr_Bob = signers[1].address;
    const adr_Charlie = signers[2].address;
    const adr_Dave = signers[3].address;
    const adr_Eric = signers[4].address;

    const ownerArray = [
        adr_Alice, adr_Bob, adr_Charlie, adr_Dave, adr_Eric
    ];

    logger.info(`Deploying contracts with the account: ${adr_Alice}`);

    const TestMultisigWallet = await hre.ethers.getContractFactory("TestMultisigWallet", { contractPath: "./contracts/TestMultisigWallet.sol" });
    const testMultisigWallet = await TestMultisigWallet.deploy(ownerArray, threshold);
    await testMultisigWallet.deployed();
    logger.info(`Contract deployed to: ${testMultisigWallet.address}`);
}

main()
    .then(() => process.exit(0))
    .catch((error) => {
        console.error(error);
        process.exit(1);
    });
```

