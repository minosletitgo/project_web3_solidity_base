#### 概念
- 带包装的ETH，简称WETH(即，Wrapped Ether)

　

#### 为什么会出现WETH
- ETH本身并不是ERC-20代币，这限制了它与许多去中心化应用（DApps）和智能合约的兼容性。
- 为了在以太坊网络上实现以太币（ETH）与其他ERC-20代币的互操作性而创建WETH。

　

#### WETH的特点：
- 兼容性：通过将ETH“包裹”成ERC-20代币，WETH可以在需要ERC-20代币的智能合约和DApp中使用。
- 流动性：WETH可以在去中心化交易所（DEX）中与其他ERC-20代币进行交易，增加了流动性。
- 简化交易：WETH的引入使得用户可以更容易地在不同代币之间进行交易和交互。
- 它实现了ERC20的必要功能，认定为遵循了ERC20标准。

　

#### 哪些项目使用WETH：
- Uniswap：用于在不同ERC-20代币之间进行交易。
- SushiSwap：类似于Uniswap的DEX，也支持WETH交易。
- Curve Finance：专注于稳定币和代币之间的流动性池。
- Aave和Compound：这些借贷平台支持以WETH作为抵押物。
- Balancer：流动性池协议，也使用WETH进行交易。

　

#### 源码：
- 合约地址：0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2 
```
pragma solidity ^0.4.18;

contract WETH9 {
    string public name     = "Wrapped Ether";
    string public symbol   = "WETH";
    uint8  public decimals = 18;

    event  Approval(address indexed src, address indexed guy, uint wad);
    event  Transfer(address indexed src, address indexed dst, uint wad);
    event  Deposit(address indexed dst, uint wad);
    event  Withdrawal(address indexed src, uint wad);

    mapping (address => uint)                       public  balanceOf;
    mapping (address => mapping (address => uint))  public  allowance;

    function() public payable {
        deposit();
    }
    function deposit() public payable {
        balanceOf[msg.sender] += msg.value;
        Deposit(msg.sender, msg.value);
    }
    function withdraw(uint wad) public {
        require(balanceOf[msg.sender] >= wad);
        balanceOf[msg.sender] -= wad;
        msg.sender.transfer(wad);
        Withdrawal(msg.sender, wad);
    }

    function totalSupply() public view returns (uint) {
        return this.balance;
    }

    function approve(address guy, uint wad) public returns (bool) {
        allowance[msg.sender][guy] = wad;
        Approval(msg.sender, guy, wad);
        return true;
    }

    function transfer(address dst, uint wad) public returns (bool) {
        return transferFrom(msg.sender, dst, wad);
    }

    function transferFrom(address src, address dst, uint wad)
        public
        returns (bool)
    {
        require(balanceOf[src] >= wad);

        if (src != msg.sender && allowance[src][msg.sender] != uint(-1)) {
            require(allowance[src][msg.sender] >= wad);
            allowance[src][msg.sender] -= wad;
        }

        balanceOf[src] -= wad;
        balanceOf[dst] += wad;

        Transfer(src, dst, wad);

        return true;
    }
}
```