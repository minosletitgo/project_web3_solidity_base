#### 概念
- 恒定总和自动做市商（CSMM, Constant Sum Market Maker）
- 在区块链的去中心化金融（DeFi）中，CSMM是常见的"做市商模型"之一。

　

#### 公式
- 恒定总和做市商的核心公式是： ```𝑥 + 𝑦 = 𝐶```
- x 和 𝑦，分别代表池中的两种代币的数量，
- C 是一个恒定值。这意味着两种代币的数量总和不变。

　

#### 特点：
- 流动性有限：一旦某种代币的数量耗尽，另一种代币就无法继续交易。因此，当池子中的某个代币被完全兑换完后，交易会停止。
- 零滑点交易：当交易量很小时，CSMM 可以提供零滑点的交易，因为两个代币的总和保持不变。
- 适用场景：由于滑点较小，这种模型适合用于稳定币之间的交易。或，流动性较为均衡的代币对交易。

　

#### 理解恒定和
- 在每次swap过程中，确实保持 ```𝑥 + 𝑦 = 𝐶```
- 在每次addLiquidity后，```𝑥 + 𝑦``` 的总量变为 𝐶^，但是，这并不意味着 ```𝑥 + 𝑦 = 𝐶``` 的关系被破坏了
- 之后仍然遵循 ```𝑥^ + 𝑦^ = 𝐶^```
- 此处表达的是，新的总量𝐶^，是更新后的常数。

　

#### 示例代码：
```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.26;

interface IERC20 {
    function totalSupply() external view returns (uint256);
    function balanceOf(address account) external view returns (uint256);
    function transfer(address recipient, uint256 amount)
        external
        returns (bool);
    function allowance(address owner, address spender)
        external
        view
        returns (uint256);
    function approve(address spender, uint256 amount) external returns (bool);
    function transferFrom(address sender, address recipient, uint256 amount)
        external
        returns (bool);
}

contract CSAMM {
    // 定义两种代币
    IERC20 public immutable token0;
    IERC20 public immutable token1;

    // 分别保存两种代币的存储数量，即"合约的代币储备量"
    uint256 public reserve0;
    uint256 public reserve1;

    // 流动性总额(它越多，代表做市商合约的流动性越大)
    uint256 public totalSupply;

    // 记录每个用户地址的"流动性份额"
    mapping(address => uint256) public balanceOf;

    constructor(address _token0, address _token1) {
        // 假设 token0 和 token1 的小数位数相同
        token0 = IERC20(_token0);
        token1 = IERC20(_token1);
    }

    // 内部函数：增加指定用户的"流动性份额"
    function _mint(address _to, uint256 _amount) private {
        balanceOf[_to] += _amount;
        totalSupply += _amount;
    }

    // 内部函数：减少指定用户的"流动性份额"
    function _burn(address _from, uint256 _amount) private {
        balanceOf[_from] -= _amount;
        totalSupply -= _amount;
    }

    // 内部函数：更新代币储备量
    function _update(uint256 _res0, uint256 _res1) private {
        reserve0 = _res0;
        reserve1 = _res1;
    }

    // 交换函数：用于交换代币(会影响"合约的代币储备量"，不会影响用户的"流动性份额")
    function swap(address _tokenIn, uint256 _amountIn)
        external
        returns (uint256 amountOut)
    {
        /*
            - 在调取前，通常会提示用户，"授权给本合约足额的代币"
        */

        require(
            _tokenIn == address(token0) || _tokenIn == address(token1),
            "invalid token"
        );
        require(_amountIn > 0, "amount in = 0");

        bool isToken0 = (_tokenIn == address(token0));

        (IERC20 tokenIn, IERC20 tokenOut, uint256 resIn, uint256 resOut) =
        isToken0
            ? (token0, token1, reserve0, reserve1)
            : (token1, token0, reserve1, reserve0);

        /*
            - 交换操作，通常会产生手续费，本示例中为"千分之三"

            - 使用"恒定"公式，作为推演出发点
            - 𝑥 + 𝑦 = 𝐶
            resIn + resOut = 𝐶

            ==> (resIn + _amountIn) + (resOut - _amountOut) = 𝐶 = resIn + resOut
            ==> _amountIn - _amountOut = 0
            ==> _amountIn - _amountOut = 0
            ==> _amountOut = _amountIn
            ==> _amountOut = _amountIn * (100% - 0.3%)
            ==> _amountOut = _amountIn * 99.7%
            ==> _amountOut = (_amountIn * 997) / 1000
        */            

        // 把用户的代币，使用"授权转账"到合约
        tokenIn.transferFrom(msg.sender, address(this), _amountIn);
        uint256 amountIn = tokenIn.balanceOf(address(this)) - resIn;

        // fee 千分之三的手续费
        amountOut = (amountIn * 997) / 1000;

        (uint256 res0, uint256 res1) = isToken0
            ? (resIn + amountIn, resOut - amountOut)
            : (resOut - amountOut, resIn + amountIn);

        // 更新本合约的余额
        _update(res0, res1);

        // 把计算出的Out，转账给用户
        tokenOut.transfer(msg.sender, amountOut);
    }

    // 添加流动性函数(会影响用户的"流动性份额"，也会影响"合约的代币储备量")
    function addLiquidity(uint256 _amount0, uint256 _amount1)
        external
        returns (uint256 shares)
    {
        /*
            - 在调取前，通常会提示用户，"授权给该合约足额的代币"
            - 用户让合约操控自己的两种代币，为自己增加了"流动性份额"，也增加了"合约的代币储备量"
        */

        // 把用户的代币，使用"授权转账"到合约
        token0.transferFrom(msg.sender, address(this), _amount0);
        token1.transferFrom(msg.sender, address(this), _amount1);

        // 获取当前合约中代币的最新余额
        uint256 bal0 = token0.balanceOf(address(this));
        uint256 bal1 = token1.balanceOf(address(this));

        // 计算新增的储备量
        uint256 d0 = bal0 - reserve0;
        uint256 d1 = bal1 - reserve1;

        /*
            - 使用"增量比例"公式，作为推演出发点
            a = 用户带来新增的代币总数          (单位：代币数量)
            L = 合约的代币储备总量             (单位：代币数量)            
            s = 需要为用户增加的"流动性份额"    (单位：流动性份额)
            T = 总流动性份额                   (单位：流动性份额)

            => 遵循同一单位做比例
            => (L + a) / L = (T + s) / T
            => s = a * T / L
        */

        if (totalSupply > 0) {
            shares = ((d0 + d1) * totalSupply) / (reserve0 + reserve1);
        } else {
            shares = d0 + d1;
        }

        require(shares > 0, "shares = 0");

        // 为用户增加(铸造)流动性份额
        _mint(msg.sender, shares);

        // 刷新代币储备量
        _update(bal0, bal1);
    }

    // 减少流动性函数(会影响用户的"流动性份额"，也会影响"合约的代币储备量")
    function removeLiquidity(uint256 _shares)
        external
        returns (uint256 d0, uint256 d1)
    {
        /*
            - 使用"减量比例"公式，作为推演出发点
            a = 最终用户减去的代币总数          (单位：代币数量)
            L = 合约的代币储备总量             (单位：代币数量)            
            s = 需要为用户减去的"流动性份额"    (单位：流动性份额)
            T = 总流动性份额                   (单位：流动性份额)

            => 遵循同一单位做比例
            => (L - a) / L = (T - s) / T
            => a / L = s / T
            => a = L * s / T
            => (d0 + d1) = (reserve0 + reserve1)* s/T
                         = reserve0*s/T + reserve1*s/T
            => d0 = reserve0*s/T
            => d1 = reserve1*s/T
        */
        d0 = (reserve0 * _shares) / totalSupply;
        d1 = (reserve1 * _shares) / totalSupply;

        // 减少(销毁)用户的流行性份额
        _burn(msg.sender, _shares);

        // 刷新代币储备量
        _update(reserve0 - d0, reserve1 - d1);

        // 返还给用户代币数量
        if (d0 > 0) {
            token0.transfer(msg.sender, d0);
        }
        if (d1 > 0) {
            token1.transfer(msg.sender, d1);
        }
    }

    // 查询指定账户的流动性份额   
    function getBalanceOf(address _account) external view returns(uint256) {
        return balanceOf[_account];
    }

    // 查询流动性总额
    function getTotalSupply() external view returns(uint256) {
        return totalSupply;
    }    
}

```

　

#### 代码重点：
- 理解"代币数量"单位
- 理解"流动性份额"单位
- 遵循 ```𝑥 + 𝑦 = 𝐶```
- 遵循 ```𝑥^ + 𝑦^ = 𝐶^```
- ```swap```操作中，会额外扣除用户的代币数量，作为手续费，储备在代币池中
- 此处，手续费会累积到流动性池中，随着时间的推移，流动性份额的价值会增加。
- ```addLiquidity```操作后，用户的流动性份额会增加
- ```removeLiquidity```操作后，用户的流动性份额会减少，同时返回给用户代币