#### 概念
- 恒定乘积自动做市商（CPMM, Constant Product Market Maker）
- 在区块链的去中心化金融（DeFi）中，CSMM是常见的"做市商模型"之一。

　

#### 公式
- 恒定总和做市商的核心公式是： 𝑥 × 𝑦 = k
- x 和 𝑦，分别代表池中的两种代币的数量，
- k 是一个恒定值。这意味着无论交易如何进行，两个代币的乘积始终保持不变。

　

#### 特点：
- 流动性保证：因为乘积始终保持恒定，所以无论池子中一种代币的数量有多少，仍然可以进行交易。这保证了流动性。
- 价格滑点：由于是恒定乘积模型，交易量越大，价格滑点（价格变化）越大。
- 适用场景：适用于大部分代币交易场景，尤其是在去中心化交易所（DEX）中广泛使用，如 Uniswap、SushiSwap 等。

　

#### 理解恒定和
- 在每次swap过程中，确实保持 ```𝑥 × 𝑦 = k```
- 在每次addLiquidity后，```𝑥 × 𝑦``` 的总量变为 𝐶^，但是，这并不意味着 ```𝑥 × 𝑦 = k``` 的关系被破坏了
- 之后仍然遵循 ```𝑥^ + 𝑦^ = k^```
- 此处表达的是，新的总量𝐶^，是更新后的常数。

　

#### 示例代码：
```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.26;

interface IERC20 {
    function totalSupply() external view returns (uint256);
    function balanceOf(address account) external view returns (uint256);
    function transfer(address recipient, uint256 amount)
        external
        returns (bool);
    function allowance(address owner, address spender)
        external
        view
        returns (uint256);
    function approve(address spender, uint256 amount) external returns (bool);
    function transferFrom(address sender, address recipient, uint256 amount)
        external
        returns (bool);
}

contract CPAMM {
    // 定义两种代币
    IERC20 public immutable token0;
    IERC20 public immutable token1;

    // 分别保存两种代币的存储数量，即"合约的代币储备量"
    uint256 public reserve0;
    uint256 public reserve1;

    // 流动性总额(它越多，代表做市商合约的流动性越大)
    uint256 public totalSupply;

    // 记录每个用户地址的"流动性份额"
    mapping(address => uint256) public balanceOf;

    constructor(address _token0, address _token1) {
        // 假设 token0 和 token1 的小数位数相同
        token0 = IERC20(_token0);
        token1 = IERC20(_token1);
    }

    // 内部函数：增加指定用户的"流动性份额"
    function _mint(address _to, uint256 _amount) private {
        balanceOf[_to] += _amount;
        totalSupply += _amount;
    }

    // 内部函数：减少指定用户的"流动性份额"
    function _burn(address _from, uint256 _amount) private {
        balanceOf[_from] -= _amount;
        totalSupply -= _amount;
    }

    // 内部函数：更新代币储备量
    function _update(uint256 _reserve0, uint256 _reserve1) private {
        reserve0 = _reserve0;
        reserve1 = _reserve1;
    }

    // 交换函数：用于交换代币(会影响"合约的代币储备量"，不会影响用户的"流动性份额")
    function swap(address _tokenIn, uint256 _amountIn)
        external
        returns (uint256 amountOut)
    {
        /*
            - 在调取前，通常会提示用户，"授权给本合约足额的代币"
        */        
        require(
            _tokenIn == address(token0) || _tokenIn == address(token1),
            "invalid token"
        );
        require(_amountIn > 0, "amount in = 0");

        bool isToken0 = _tokenIn == address(token0);
        (IERC20 tokenIn, IERC20 tokenOut, uint256 reserveIn, uint256 reserveOut)
        = isToken0
            ? (token0, token1, reserve0, reserve1)
            : (token1, token0, reserve1, reserve0);

        /*
            - 交换操作，通常会产生手续费，本示例中为"千分之三"

            - 使用"恒定"公式，作为推演出发点
            =>  𝑥 × 𝑦 = k
            =>  (𝑥 + △𝑥) × (𝑦 - △𝑦) = k
            =>  △𝑦 = (△𝑥 × 𝑦) / (𝑥 + △𝑥)
            =>  △𝑦 = (△𝑥 × 𝑦 x (100% - 0.3%)) / (𝑥 + △𝑥)
            =>  △𝑦 = (△𝑥 × 𝑦 x 99.7%) / (𝑥 + △𝑥)
            =>  △𝑦 = (△𝑥 × 𝑦 x 997) / (𝑥 + △𝑥) x 1000
        */             

        // 把用户的代币，使用"授权转账"到合约
        tokenIn.transferFrom(msg.sender, address(this), _amountIn);

        // fee 千分之三的手续费
        amountOut = _amountIn * reserveOut * 997 / (reserveIn + _amountIn) * 1000

        // 把计算出的Out，转账给用户
        tokenOut.transfer(msg.sender, amountOut);

        // 更新本合约的余额
        _update(
            token0.balanceOf(address(this)), token1.balanceOf(address(this))
        );
    }

    // 添加流动性函数(会影响用户的"流动性份额"，也会影响"合约的代币储备量")
    function addLiquidity(uint256 _amount0, uint256 _amount1)
        external
        returns (uint256 shares)
    {
        /*
            - 在调取前，通常会提示用户，"授权给该合约足额的代币"
            - 在调取前，通常会提示用户只需要输入一种代币的数量，而另一种代币的数量会根据当前的储备比例自动计算。这样就避免了用户需要手动计算比例的问题，使得过程更加直观和用户友好。
            - 用户让合约操控自己的两种代币，为自己增加了"流动性份额"，也增加了"合约的代币储备量"

            - 基础数据介绍
            L0 = √(𝑥 x 𝑦)                           = "开根号"，代表当前流动性池子的价值         (单位：流动性份额)
            L1 = √((𝑥 + △𝑥) × (𝑦 + △𝑦))            = "开根号"，代表添加代币后流动性池子的价值    (单位：流动性份额)
            shares                                  = 需要为用户增加的"流动性份额"               (单位：流动性份额)
            totalSupply                             = 总流动性份额                              (单位：流动性份额)

            - 使用"比例恒定"公式，来限制 输入代币量 的关系：
            =>  (𝑥 + △𝑥) × (𝑦 + △𝑦) = k
            =>  𝑥 / 𝑦 = (𝑥 + △𝑥) / (𝑦 + △𝑦)
            =>  △𝑥 = △𝑦 x 𝑥 / 𝑦
            =>  △𝑦 = △𝑥 x 𝑦 / 𝑥

            - "流动性对等"公式
            => L0 / L1 = totalSupply / (totalSupply + shares)
            => shares = totalSupply x (L1 - L0) / L0
            => shares = 省去推导过程..........
            => shares = totalSupply x ( △𝑦 x √(y) ) / y
        */

        if (reserve0 > 0 || reserve1 > 0) {
            require(
                reserve0 * _amount1 == reserve1 * _amount0, "x / y != dx / dy"
            );
        }        

        // 把用户的代币，使用"授权转账"到合约
        token0.transferFrom(msg.sender, address(this), _amount0);
        token1.transferFrom(msg.sender, address(this), _amount1);

        /*
        How much dx, dy to add?

        xy = k
        (x + dx)(y + dy) = k'

        No price change, before and after adding liquidity
        x / y = (x + dx) / (y + dy)

        x(y + dy) = y(x + dx)
        x * dy = y * dx

        x / y = dx / dy
        dy = y / x * dx
        */


        /*
        How much shares to mint?

        f(x, y) = value of liquidity
        We will define f(x, y) = sqrt(xy)

        L0 = f(x, y)
        L1 = f(x + dx, y + dy)
        T = total shares
        s = shares to mint

        Total shares should increase proportional to increase in liquidity
        L1 / L0 = (T + s) / T

        L1 * T = L0 * (T + s)

        (L1 - L0) * T / L0 = s 
        */

        /*
        Claim
        (L1 - L0) / L0 = dx / x = dy / y

        Proof
        --- Equation 1 ---
        (L1 - L0) / L0 = (sqrt((x + dx)(y + dy)) - sqrt(xy)) / sqrt(xy)
        
        dx / dy = x / y so replace dy = dx * y / x

        --- Equation 2 ---
        Equation 1 = (sqrt(xy + 2ydx + dx^2 * y / x) - sqrt(xy)) / sqrt(xy)

        Multiply by sqrt(x) / sqrt(x)
        Equation 2 = (sqrt(x^2y + 2xydx + dx^2 * y) - sqrt(x^2y)) / sqrt(x^2y)
                   = (sqrt(y)(sqrt(x^2 + 2xdx + dx^2) - sqrt(x^2)) / (sqrt(y)sqrt(x^2))
        
        sqrt(y) on top and bottom cancels out

        --- Equation 3 ---
        Equation 2 = (sqrt(x^2 + 2xdx + dx^2) - sqrt(x^2)) / (sqrt(x^2)
        = (sqrt((x + dx)^2) - sqrt(x^2)) / sqrt(x^2)  
        = ((x + dx) - x) / x
        = dx / x

        Since dx / dy = x / y,
        dx / x = dy / y

        Finally
        (L1 - L0) / L0 = dx / x = dy / y
        */
        if (totalSupply == 0) {
            shares = _sqrt(_amount0 * _amount1);
        } else {
            shares = _min(
                (_amount0 * totalSupply) / reserve0,
                (_amount1 * totalSupply) / reserve1
            );
        }
        require(shares > 0, "shares = 0");
        _mint(msg.sender, shares);

        _update(
            token0.balanceOf(address(this)), token1.balanceOf(address(this))
        );
    }

    function removeLiquidity(uint256 _shares)
        external
        returns (uint256 amount0, uint256 amount1)
    {
        /*
        Claim
        dx, dy = amount of liquidity to remove
        dx = s / T * x
        dy = s / T * y

        Proof
        Let's find dx, dy such that
        v / L = s / T
        
        where
        v = f(dx, dy) = sqrt(dxdy)
        L = total liquidity = sqrt(xy)
        s = shares
        T = total supply

        --- Equation 1 ---
        v = s / T * L
        sqrt(dxdy) = s / T * sqrt(xy)

        Amount of liquidity to remove must not change price so 
        dx / dy = x / y

        replace dy = dx * y / x
        sqrt(dxdy) = sqrt(dx * dx * y / x) = dx * sqrt(y / x)

        Divide both sides of Equation 1 with sqrt(y / x)
        dx = s / T * sqrt(xy) / sqrt(y / x)
           = s / T * sqrt(x^2) = s / T * x

        Likewise
        dy = s / T * y
        */

        // bal0 >= reserve0
        // bal1 >= reserve1
        uint256 bal0 = token0.balanceOf(address(this));
        uint256 bal1 = token1.balanceOf(address(this));

        amount0 = (_shares * bal0) / totalSupply;
        amount1 = (_shares * bal1) / totalSupply;
        require(amount0 > 0 && amount1 > 0, "amount0 or amount1 = 0");

        _burn(msg.sender, _shares);
        _update(bal0 - amount0, bal1 - amount1);

        token0.transfer(msg.sender, amount0);
        token1.transfer(msg.sender, amount1);
    }

    function _sqrt(uint256 y) private pure returns (uint256 z) {
        if (y > 3) {
            z = y;
            uint256 x = y / 2 + 1;
            while (x < z) {
                z = x;
                x = (y / x + x) / 2;
            }
        } else if (y != 0) {
            z = 1;
        }
    }

    function _min(uint256 x, uint256 y) private pure returns (uint256) {
        return x <= y ? x : y;
    }
}
```
- $shares = totalSupply · (\frac{△y}{y})$