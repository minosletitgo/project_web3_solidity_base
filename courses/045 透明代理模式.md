#### 基本理解：
- 充分理解"可升级代理模式"与"函数选择器冲突"后，可得出如下假设：
- 1."代理合约"显性定义的函数只有"升级函数"，仅此一个。
- 2."逻辑合约"会以默认方式，定义大量的逻辑函数。
- 3.所以，"代理合约"的"升级函数"的函数选择器，是有可能与"逻辑合约"的"某个逻辑函数(假如，```foo(address)```)"的函数选择器，巧合为一个值。
- 4.此时，当管理员向"代理合约"发起```foo(address)```调用(使用低级调用方式)时，会直接错误的调用到"代理合约的升级函数"，那么，结果就是，代理合约升级为了一个"黑洞合约"，后果完全不可设想。
- 5.这种"函数选择器一致"导致的严重后果，称之为"代理模式下的函数选择器冲突"。
- 6.解决这种冲突的方式有两个：透明代理(Transparent Proxy Standard) 与 通用可升级代理(Universal Upgradeable Proxy Standard)。

　

#### 透明代理的思想：
- 既然，只有"管理员才会发起冲突式的调用"，他的原意是"向逻辑合约调用函数"，但是"错误调用到了升级函数"。
- 此时，直接"限制管理员，不让他调用逻辑合约的函数"，也就是管理员只能调用升级函数，就可以避免冲突。

　

-----------------------------------------------------------------

　

混合代码 - MixContract.sol
```
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "MyScripts/utils/StorageSlot.sol";

contract Proxy {
    // 代理合约中，使用高位存储槽(彻底与逻辑合约的变量定义，隔离开) 记录逻辑合约地址，以及管理员地址
    bytes32 private constant IMPLEMENTATION_SLOT = bytes32(uint256(keccak256("eip1967.proxy.implementation")));
    bytes32 private constant ADMIN_SLOT = bytes32(uint256(keccak256("eip1967.proxy.admin")));

    ////////////////////////////////////////////////////

    modifier onlyAdmin() {
        require(msg.sender == getAdmin());
        _;
    }

    constructor(address logicContractAddress) {
        setImplementation(logicContractAddress);
        setAdmin(msg.sender);
    }

    function setImplementation(address newImplementation) internal {
        require(
            newImplementation != address(0),
            "New implementation is the zero address"
        );

        StorageSlot.getAddressSlot(IMPLEMENTATION_SLOT).value = newImplementation;
    }

    // function getImplementation() internal view returns (address) {
    //     address impl;
    //     bytes32 position = IMPLEMENTATION_SLOT;
    //     assembly {
    //         impl := sload(position)
    //     }
    //     return impl;
    // }

    function getImplementation() internal view returns (address) {
        address impl = StorageSlot.getAddressSlot(IMPLEMENTATION_SLOT).value;
        return impl;
    }    

    function setAdmin(address newAdmin) internal {
        require(newAdmin != address(0), "new Admin is the zero address");

        StorageSlot.getAddressSlot(ADMIN_SLOT).value = newAdmin;
    }

    // function getAdmin() internal view returns (address) {
    //     address admin;
    //     bytes32 position = ADMIN_SLOT;
    //     assembly {
    //         admin := sload(position)
    //     }
    //     return admin;
    // }

    function getAdmin() internal view returns (address) {
        address admin = StorageSlot.getAddressSlot(ADMIN_SLOT).value;
        return admin;
    }    

    function upgradeTo(address logicContractAddress) external onlyAdmin {
        // 升级 逻辑合约
        require(msg.sender == getAdmin());
        setImplementation(logicContractAddress);
    }

    fallback() external payable {
        // 管理员发起的调用，不得转发到逻辑合约
        require(msg.sender != getAdmin());
        _delegate_02(getImplementation());
    }

    receive() external payable {}

    // function _delegate(address impl) internal {
    //     (bool suc, bytes memory data) = impl.delegatecall(msg.data);
    //     if (!suc) {
    //         revert("_delegate Failed!");
    //     }
    // }

    function _delegate_02(address impl) internal {
        assembly {
            calldatacopy(0, 0, calldatasize())
            let result := delegatecall(gas(), impl, 0, calldatasize(), 0, 0)
            returndatacopy(0, 0, returndatasize())
            switch result
            case 0 {
                revert(0, returndatasize())
            }
            default {
                return(0, returndatasize())
            }
        }
    }

    /*
        以上 fallback调用 _delegate 与 _delegate_02 的差异

        返回值差异：
        1. 上层的fallback，没有返回值机制，bytes memory data并没有返回出去
        2. 使用汇编的方式，绕开"没有返回值机制"，操作内存做到返回机制。

        可控性与效率差异：
        1. 简洁，语法更易读，但会增加一些 Solidity 自带的错误信息开销。
        2. 直接在内存中处理数据，并确保与被代理的合约返回数据一致，减少额外的开销，提高了效率。
        
        错误信息的传播差异:
        1. 抛出的是自定义的错误 "delegate Failed!"，原始的错误数据不会返回给调用者。
        2. 保留原始合约的返回信息或错误信息，便于调试。

        总结：
        第二种实现方式在高级代理合约中更为常用，因为它可以确保返回数据或错误信息与原始合约一致，且具有更高的效率。
    */
}

contract LogicV1 {
    uint256 public _value1;
    uint256 public _value2;
    uint256 public _value3;

    ////////////////////////////////////////////////////

    event OnSetValue1(uint256 value);
    event OnSetValue2(uint256 value);
    event OnSetValue3(uint256 value);

    //函数选择器，且参数为1，合并编码:  0x6ca0f3210000000000000000000000000000000000000000000000000000000000000001
    function setValue1(uint256 value) external returns (uint256) {
        _value1 = value;
        emit OnSetValue1(value);
        return value;
    }

    //函数选择器:  0x60d586f8000000000000000000000000000000000000000000000000000000000000007b
    function getValue1() external view returns (uint256) {
        return _value1;
    }    

    //函数选择器，且参数为2，合并编码:  0x74d393f00000000000000000000000000000000000000000000000000000000000000002
    function setValue2(uint256 value) external returns (uint256) {
        _value2 = value;
        emit OnSetValue2(value);
        return value;
    }

    //函数选择器:  0xc515205d000000000000000000000000000000000000000000000000000000000000007b
    function getValue2() external view returns (uint256) {
        return _value2;
    }    

    //函数选择器，且参数为3，合并编码:  0x9a68e5820000000000000000000000000000000000000000000000000000000000000003
    function setValue3(uint256 value) external returns (uint256) {
        _value3 = value;
        emit OnSetValue3(value);
        return value;
    }

    //函数选择器:  0xf2d14b00000000000000000000000000000000000000000000000000000000000000007b
    function getValue3() external view returns (uint256) {
        return _value3;
    }    
}

contract LogicV2 is LogicV1 {
    uint256 public _value4;

    ////////////////////////////////////////////////////

    event OnSetValue4(uint256 value);

    //函数选择器，且参数为123，合并编码:  0xd69da761000000000000000000000000000000000000000000000000000000000000007b
    function setValue4(uint256 value) external virtual returns (uint256) {
        _value4 = value;
        emit OnSetValue4(value);
        return value;
    }

    //函数选择器:  0xbfd68858000000000000000000000000000000000000000000000000000000000000007b
    function getValue4() external view returns (uint256) {
        return _value4;
    }      
}


```

　

-----------------------------------------------------------------


　

#### 解释：
- 代码改动很小，只需要"限制管理员，调用```fallback()```即可"。
- 透明代理模式的缺点：每次用户调用到```fallback()```时，都需要判断"不许是管理员"，这会消耗更多的gas。
- 适用：对安全性和权限控制要求高的复杂项目（如 DeFi、DAO），能够清晰分离用户和管理员的权限。


　
#### 历史背景
- 透明代理模式是在智能合约社区中较早发展出来的一种模式，旨在解决合约部署后无法更改的问题。
- 它允许合约逻辑在不改变合约地址的情况下进行升级。
- 何为"透明"：对于外部观察者来说，代理合约看起来就像是实现了所有功能的合约本身，所有的函数调用和事件都像是直接从代理合约发出的一样。
- 随着智能合约应用的发展，社区意识到需要一种更简单、更标准的方式来处理合约升级问题。因此提出了UUPS，它将升级逻辑内置到实现合约中，简化了升级流程。


